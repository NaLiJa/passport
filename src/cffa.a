;-------------------------------
; EnterCFFAIfAvailable
; check for, and potentially call, the CFFA 3000
; administration menu
;
; currently only supports CFFA 3000 in slot 7
; (no slot scan)
;
; in:    none
; out:   C clear if we found CFFA 3000 in slot 7 and called it
;        C set if we didn't find CFFA 3000
;-------------------------------
EnterCFFAIfAvailable
         ldy   #$04
-        lda   $C7F6, y
         cmp   @CFFASignature, y
         sec
         bne   +
         dey
         bpl   -
         jsr   $C730
         clc
+        rts
@CFFASignature
         !byte $43,$46,$33,$4B,$31
