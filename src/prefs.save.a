;-------------------------------
; SavePrefs
; save preferences to file
;
; in:  ProDOS must be in memory
;-------------------------------
!macro SavePrefs {
         lda   gChangedPrefs
         bne   +
         lda   #<PREFSFILE
         sta   mliparam+1
         lda   #>PREFSFILE
         sta   mliparam+2
         jsr   DeleteFile ; don't care if this fails
         lda   #FULL_ACCESS
         sta   mliparam+3
         lda   #$04       ; text file type
         sta   mliparam+4
         lda   #$00
         sta   mliparam+5 ; no aux file type
         sta   mliparam+6
         jsr   CreateFile
         bcs   +
         lda   #<PREFSBUFFER
         sta   mliparam+3
         lda   #>PREFSBUFFER
         sta   mliparam+4
         jsr   OpenFile
         bcs   +
         ldy   #<PREFSVER
         sty   mliparam+2
         ldy   #>PREFSVER
         sty   mliparam+3
         ldy   #<PREFSWRITELEN
         sty   mliparam+4
         ldy   #>PREFSWRITELEN
         sty   mliparam+5
         jsr   WriteFile
         jsr   CloseFile
+
}
