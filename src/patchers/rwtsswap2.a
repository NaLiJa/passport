;-------------------------------
; #RWTSSWAP2
; after DOS loads, program calls
; subroutines at $BA69 and $BA7D
; to swap RWTS parameters on the
; fly
; e.g. Mask Parade
;-------------------------------
!zone {
_rwtsswap2
         lda   gIsBoot0   ; if DOS 3.3 boot0 loader
         bne   .exit
         lda   #$04
         ldx   #$69
         ldy   #$13
         jsr   compare    ; and T00,S04,$69 ==
         !byte $A9,$AB
         !byte $8D,$4B,$B8
         !byte $A9,$FF
         !byte $20,$DF,$BC
         !byte $20,$EC,$BC
         !byte $A9,$AA
         !byte $8D,$29,$BA
         !byte $60
         bcs   .exit
         ldx   #$7D
         ldy   #$15
         jsr   compare    ; and T00,S04,$7D ==
         !byte $A9,$FF
         !byte $8D,$4B,$B8
         !byte $A9,$DE
         !byte $20,$DF,$BC
         !byte $A9,$AA
         !byte $20,$EC,$BC
         !byte $A9,$96
         !byte $8D,$29,$BA
         !byte $60
         bcs   .exit
         ldx   #$69
         ldy   #$01
         jsr   modify     ; then set T00,S04,$69 =
         !byte $60
         lda   #$04
         ldx   #$7D
         ldy   #$01
         jsr   modify     ; and set T00,S04,$7D =
         !byte $60
.exit
}
