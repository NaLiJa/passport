;-------------------------------
; #DISKVOL
; non-standard disk volume prevents
; disk from loading its own files
; or even its own DOS
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   .exit

         lda   #$01
         ldx   #$EE
         tay
         jsr   compare    ; and T00,S01,$EE ==
         !byte $FB
         bcs   .exit

         ldx   #$FB
         ldy   #$04
         jsr   compare    ; and T00,S01,$FB ==
         !byte $00,$01,$EF,$D8
         bcs   .exit

         lda   gForceDiskVol
         beq   .yes

         lda   gIsPanglosDOS
         ora   gCapturedDiskVolumeNumber
         bne   +
         lda   gDiskVolumeNumber
         cmp   #$FE
         bne   .yes
+
         ldx   #$EB
         ldy   #$01
         jsr   compare    ; and T00,S01,$EB !=
         !byte $FE
         bcc   .exit

         ldy   #$01
         jsr   compare    ; and T00,S01,$EB !=
         !byte $00
         bcc   .exit
.yes
         lda   #$08
         ldx   #$10
         ldy   #$05
         jsr   compare    ; and T00,S08,$10 ==
         !byte $A0,$03    ; LDY #$03
         !byte $B1,$48    ; LDA ($48),Y
         !byte $48        ; PHA
         bcc   .veryyes

         ldx   #$16       ; or T00,S08,$16 == the same thing
         ldy   #$05       ; (variation used by PanglosDOS)
         jsr   compare    ; preserves A/X
         !byte $A0,$03
         !byte $B1,$48
         !byte $48
         bcs   .exit

.veryyes
         lda   #s_diskvol
         jsr   PrintByID  ; preserves X

         lda   #$08
         inx
         inx              ; now $12 or $18 (depending on which compare matched)
         ldy   #$02
         jsr   modify     ; then set T00,S08,$12/18 =
         !byte $A9,$00    ; LDA #$00
.exit
}
