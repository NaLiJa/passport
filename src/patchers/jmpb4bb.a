;-------------------------------
; #JMPB4BB
; boot2 jumps to $B4BB for a
; nibble check
; e.g. Troll's Tale
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gTrack
         cmp   #$02
         bne   .exit
         lda   gIsDOS32
         bne   +
         lda   #$0A
         !byte $2C        ; hide next LDA
+        lda   #$03
         sta   gDisplayBytes
         ldx   #$D7
         ldy   #$04
         jsr   compare    ; and T02,S0x,$D7 ==
         !byte $D8        ; CLD
         !byte $AD,$FF,$CF; LDA $CFFF
         bcc   .found
         ldx   #$DD
         ldy   #$04
         jsr   compare    ; or T02,S0x,$DD ==
         !byte $D8        ; CLD
         !byte $AD,$FF,$CF; LDA $CFFF
         bcs   .exit
.found
         pha
         lda   #s_jmpb4bb
         jsr   PrintByID
         pla
         inx
         ldy   #$03
         jsr   modify     ; then set offset+1 to
         !byte $4C,$84,$9D
.exit
}
