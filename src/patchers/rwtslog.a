;-------------------------------
; #RWTSLOG
; Print log messages for RWTS variants
; that indicate structural copy protection
; but don't require any patches
;-------------------------------

!zone {
;-------------------------------
; RWTS "LSR $6A" identification
; disk allows either $D4 or $D5 for the
; first address prologue nibble
;-------------------------------
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   +
         lda   #$03
         sta   gDisplayBytes
         ldx   #$4F
         ldy   #$0A
         jsr   compare    ; and T00,S03,$4F ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $4A
         !byte WILDCARD,$6A
         !byte $D0,$EF
         bcs   +
         lda   #s_lsr6a
         jsr   PrintByID
         inc   gPatchCount
+

;-------------------------------
; RWTS "CMP #$08 / BCS" identification
; disk allows either $DE or a timing bit
; for the first address epilogue nibble
;-------------------------------
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   +
         lda   #$03
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$15
         jsr   compare    ; and T00,S03,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,$DE
         !byte $F0,$0A
         !byte $48
         !byte $68
         !byte $BD,$8C,$C0
         !byte $C9,$08
         !byte $B0,$A5
         !byte $EA
         !byte $18
         !byte $60
         bcs   +
         lda   #s_bcs08
         jsr   PrintByID
         inc   gPatchCount
+

;-------------------------------
; RWTS "CMP / BNE 0" identification
; disk allows any value for
; for the first address epilogue nibble
;-------------------------------
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   +
         lda   #$03
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$09
         jsr   compare    ; and T00,S03,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,WILDCARD
         !byte $D0,$00
         bcs   +
         lda   #s_cmpbne0
         jsr   PrintByID
         inc   gPatchCount
+

;-------------------------------
; RWTS "CMP / BNE 0" identification (David-DOS variant)
; disk allows any value for
; for the first address epilogue nibble
;-------------------------------
         lda   gIsDavidDOS ; if David-DOS bootloader
         bne   +
         lda   #$05
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$09
         jsr   compare    ; and T00,S05,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,WILDCARD
         !byte $D0,$00
         bcs   +
         lda   #s_cmpbne0
         jsr   PrintByID
         inc   gPatchCount
+
}
