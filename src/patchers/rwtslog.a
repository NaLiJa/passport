;-------------------------------
; #RWTSLOG
; Print log messages for RWTS variants
; that indicate structural copy protection
; but don't require any patches
;-------------------------------

!zone {
;-------------------------------
; RWTS "LSR $6A" identification
; disk allows either $D4 or $D5 for the
; first address prologue nibble
;-------------------------------
; DOS 3.3 variant
         lda   #$03
         sta   gDisplayBytes
         ldx   #$4F
         ldy   #$0A
         jsr   compare    ; if T00,S03,$4F ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $4A
         !byte WILDCARD,$6A
         !byte $D0,$EF
         bcc   .yeslsr
; RDOS variant
         ldx   #$4D
         ldy   #$0A
         jsr   compare    ; or T00,S03,$4D ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $4A
         !byte WILDCARD,$6A
         !byte $D0,$EF
         bcs   .endlsr
.yeslsr
         lda   #s_lsr6a
         jsr   PrintByID
         inc   gPatchCount
.endlsr

;-------------------------------
; RWTS "CMP #$08 / BCS" identification
; disk allows either $DE or a timing bit
; for the first address epilogue nibble
;-------------------------------
; DOS 3.3 variant
         lda   #$03
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$15
         jsr   compare    ; if T00,S03,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,$DE
         !byte $F0,$0A
         !byte $48
         !byte $68
         !byte $BD,$8C,$C0
         !byte $C9,$08
         !byte $B0,$A5
         !byte $EA
         !byte $18
         !byte $60
         bcc   .yesbcs
; RDOS variant
         ldx   #$89
         ldy   #$11
         jsr   compare    ; or T00,S03,$89 ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,$DE
         !byte $F0,$09
         !byte $48
         !byte $68
         !byte $BD,$8C,$C0
         !byte $C9,$80
         !byte $B0
         bcs   .endbcs
.yesbcs
         lda   #s_bcs08
         jsr   PrintByID
         inc   gPatchCount
.endbcs

;-------------------------------
; RWTS "CMP / BNE 0" identification
; disk allows any value for
; for the first address epilogue nibble
;-------------------------------
; DOS 3.3 variant
         lda   #$03
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$09
         jsr   compare    ; if T00,S03,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,WILDCARD
         !byte $D0,$00
         bcc   .yesbne0
; David-DOS variant
         lda   #$05
         sta   gDisplayBytes
         ldx   #$8B
         ldy   #$09
         jsr   compare    ; or T00,S05,$8B ==
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,WILDCARD
         !byte $D0,$00
         bcs   .endbne0
.yesbne0
         lda   #s_cmpbne0
         jsr   PrintByID
         inc   gPatchCount
.endbne0
}
