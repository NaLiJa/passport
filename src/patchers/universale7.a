;-------------------------------
; #UNIVERSALE7
; replace remnants of E7 bitstream
; with a compatible BYTEstream that
; fools most E7 protection checks
; (invented by qkumba, see PoC||GTFO 0x11
; and 4am crack no. 655 Rocky's Boots 4.0)
; (updated 2021 with a more compatible
; version to fix issues on Apple //c and
; Apple IIgs)
;-------------------------------
!zone {
universale7
         lda   #BASEPAGE+$0F
         sta   .sector+2
.search  ldx   #$A0
         lda   #$00
.eor     eor   #$AC
.sector  cmp   $D100,x    ; modified at runtime
         bne   .nextsector
         inx
         cpx   #$80
         bcc   .sector
         bne   .eor

         lda   .sector+2  ; passport-test-suite/Rocky's Boots.woz [C=1] reaches here
         and   #$0F
         sta   gDisplayBytes
         jsr   PrintByID
         !byte s_e7
         ldx   #$A4
         ldy   #$0C
         jsr   modify
         !byte $78,$A8,$58,$9C,$30,$C0,$04,$A8,$58,$9C,$58,$A8
.nextsector
         dec   .sector+2
         lda   .sector+2
         cmp   #BASEPAGE-1
         bne   .search
.exit
}
