;-------------------------------
; #JMPBECA
; RWTS calls $3ECA or $BECA after reading
; data prologue to burn cycles
;
; NOTE: must be run before RWTS patchers due to overlapping bytes
;
; see jmpbeb1.a for compatibility list
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   .exit
         lda   #$02
         ldx   #$F4
         ldy   #$0D
         jsr   compare    ; and T00,S02,$F4 ==
         !byte $EA
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,$AD
         !byte $D0,$E8
         !byte $4C,$CA,WILDCARD
         bcc   .yes       ; passport-test-suite/Easy as ABC.woz [C=0] matches
;
; variant seen in Library Magic
;
         ldy   #$0D
         jsr   compare    ; or T00,S02,$F4 ==
         !byte $4C,$CA,WILDCARD
         !byte $EA
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $49
         !byte $AD,$D0,$DD
         bcs   .exit      ; passport-test-suite/Library Magic.woz [C=0] matches

.yes
         lda   #s_jmpbeca ; passport-test-suite/Spelling Rules.woz [C=0] also reaches here
         jsr   PrintByID
         lda   #$02
         ldy   #$0D
         jsr   modify     ; then set T00,S02,$F4 =
         !byte $A0,$56
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $C9,$AD
         !byte $D0,$E7
         !byte $A9,$00
.exit
}
