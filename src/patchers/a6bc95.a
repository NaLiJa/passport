;-------------------------------
; #A6BC95
; nibble count after "A6 BC 95" prologue
; e.g. The Secrets of Science Island
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gIsPascal  ; only ever seen this protection
         bne   .exit      ; on Pascal disks

         ldy   #$09
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$A6    ; CMP #$A6
         !byte $D0,$ED    ; BNE -$ED
         bcs   .exit
         ldy   #$07
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$BC    ; CMP #$BC
         bcs   .exit
         ldy   #$07
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$95    ; CMP #$95
         bcs   .exit
         ldy   #$04
         jsr   SearchTrack
         !byte $AE,$F8,$01; LDX $01F8
         !byte $A9
         bcs   .exit
         sta   gDisplayBytes
         pha
         lda   #s_a6bc95
         jsr   PrintByID
         pla
         ldy   #$01
         jsr   modify
         !byte $60        ; RTS
.exit
}
