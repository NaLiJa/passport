;-------------------------------
; #A6BC95
; nibble count after "A6 BC 95" prologue
; e.g. The Secrets of Science Island
;-------------------------------
!zone {
_a6bc95
         lda   gIsPascal  ; only ever seen this protection
         bne   .exit      ; on Pascal disks

         ldy   #$09
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$A6    ; CMP #$A6
         !byte $D0,$ED    ; BNE -$ED
         bcs   .exit
         ldy   #$09
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$BC    ; CMP #$BC
         !byte $D0,$E4    ; BNE -$E4
         bcs   .exit
         ldy   #$09
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$95    ; CMP #$95
         !byte $D0,$DB    ; BNE -$DB
         bcs   .exit
         ldy   #$0E
         jsr   SearchTrack
         !byte $AE,$F8,$01; LDX $01F8
         !byte $A9,$0A    ; LDA #$0A
         !byte $8D,$FE,$01; STA $01FE
         !byte $BD,$8E,$C0; LDA $C08E,X
         !byte $BD,$89,$C0; LDA $C089,X
         bcs   .exit
         sta   gDisplayBytes
         pha
         lda   #s_a6bc95
         jsr   PrintByID
         pla
         ldy   #$01
         jsr   modify
         !byte $60        ; RTS
.exit
}
