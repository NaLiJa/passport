;-------------------------------
; #DOS32
; construct new bootloader and RWTS for converted DOS 3.2 disks
;-------------------------------
!zone {
         lda   gIsDOS32
         bne   .exit
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit

; sectors 2-9

         lda   #>universalrwts
         ldx   #$12       ; #BASEPAGE+2
         ldy   #$08
         jsr   CopyMemory
         lda   #$38
         sta   $1656      ; #BASEPAGE+6

         lda   #s_rwtswrite
         jsr   PrintByID

; sector 1

         lda   callrwts+2
         sec
         sbc   #$06       ; #$B7 or #$37
         ldx   #$11       ; #BASEPAGE+1
         ldy   #$01
         jsr   CopyMemory
         lda   #$01
         ldx   #$38
         ldy   #$02
         jsr   compare
         !byte $20,$93
         bcs   +
         ldy   #$01
         jsr   modify
         !byte $2C
+

; sector 0

         lda   callrwts+2
         cmp   #$3D
         beq   +
         lda   #<SD_DOS32
         ldx   #>SD_DOS32
         jsr   ConstructStandardDelivery
         jmp   ++

+        lda   #<SD_DOS32LO
         ldx   #>SD_DOS32LO
         jsr   ConstructStandardDelivery

++       lda   #s_bootwrite
         jsr   PrintByID
         inc   gPatchCount
.exit
}
