;-------------------------------
; #BADEMU2
; RWTS checks for timing bit by
; checking if data latch is still
; $D5 after waiting "too long"
; but this confuses legacy emulators
; (AppleWin, older versions of MAME)
; so we patch it for compatibility
; e.g. Dinosaur Days
;-------------------------------
!zone {
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   .exit
         lda   #$03
         ldx   #$4F
         ldy   #$11
         jsr   compare    ; and T00,S03,$4F ==
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $4A        ; LSR
         !byte $C9,$6A    ; CMP #$6A
         !byte $D0,$EF    ; BNE -$EF
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $C9,$D5    ; CMP #$D5
         !byte $F0,$12    ; BEQ +$12
         bcs   .exit
         lda   #$03
         ldx   #$59
         ldy   #$02
         jsr   modify     ; then set T00,S03,$59 =
         !byte $F0,$05    ; BEQ +$05
.exit
}
