;-------------------------------
; #JMPB660
; RWTS jumps to $B660 for check for timing bits
;
; in:    gTrack = 0
;        Caller found DOS 3.3 RWTS
;        A=02,X=F6,Y=03
;
; e.g. Analogies Tutorial, Verb Viper
;-------------------------------
T00_JMPB660
;
; RWTS jumps to $B660 instead of
; checking third data prologue
;
         jsr   compare    ; if T00,S02,$F6 ==
         !byte $4C,$60,$B6
         bcs   +          ; passport-test-suite/Analogies Tutorial.woz [C=0] matches
         jsr   PrintByID
         !byte s_jmpb660
         jsr   modify     ; then set T00,S02,$F6 =
         !byte $BD,$8C,$C0
;
; RWTS swapper forcibly reinstalls a jump to $B720 instead of writing
; the third data prologue
;
         lda   #$0B
         ldx   #$08
         ldy   #$06
         jsr   compare    ; and T00,S0B,$08 ==
         !byte $4C,$20,$B7
         !byte $4C,$0E,$A9
         bcs   +          ; passport-test-suite/Analogies Tutorial.woz [C=0] matches
         jsr   modify     ; then set T00,S0B,$08 =
         !byte $BD,$8C,$C0
         !byte $20,$B8,$B8
+
