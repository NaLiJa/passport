;-------------------------------
; #PRODOS6A
; disk allows either $D4 or $D5 for the
; first address prologue nibble
; (no patches required, but we want to
; log it and pretend we patched something
; so we get a nice CRACK COMPLETE message)
;-------------------------------
!zone {
         lda   gIsProDOS  ; only if ProDOS (separate check for DOS)
         bne   .exit
         lda   #$0F
         sta   .sector+1
.sector  lda   #$FF       ; modified at runtime
         sta   gDisplayBytes
         ldx   #$A5
         ldy   #$08
         jsr   compare
         !byte $BD,$8C,$C0
         !byte $10,$FB
         !byte $4A
         !byte $C9,$6A
         bcs   +
         lda   #s_lsr6a
         jsr   PrintByID
         inc   gPatchCount
+
         lda   .sector+1
         ldx   #$EF
         ldy   #$07
         jsr   compare
         !byte $48
         !byte $68
         !byte $BD,$8C,$C0
         !byte $C9,$08
         bcs   .nextsector
         lda   #s_bcs08
         jsr   PrintByID
         inc   gPatchCount
.nextsector
         dec   .sector+1
         bpl   .sector
.exit
}
