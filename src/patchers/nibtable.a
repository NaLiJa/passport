;-------------------------------
; #NIBTABLE
; detect and fix non-standard
; nibble-to-byte translation
; e.g. Travels with Za-Zoom
;-------------------------------
!zone {
_nibtable
         jmp   .checkread

.writedata
         !byte $96,$97,$9A,$9B,$9D,$9E,$9F,$A6
         !byte $A7,$AB,$AC,$AD,$AE,$AF,$B2,$B3
         !byte $B4,$B5,$B6,$B7,$B9,$BA,$BB,$BC
         !byte $BD,$BE,$BF,$CB,$CD,$CE,$CF,$D3
         !byte $D6,$D7,$D9,$DA,$DB,$DC,$DD,$DE
         !byte $DF,$E5,$E6,$E7,$E9,$EA,$EB,$EC
         !byte $ED,$EE,$EF,$F2,$F3,$F4,$F5,$F6
         !byte $F7,$F9,$FA,$FB,$FC,$FD,$FE,$FF

.checkread
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gIsRWTS    ; if DOS 3.3 RWTS
         bne   .exit
         lda   #$02
         ldx   #$6D
         ldy   #$03
         jsr   compare    ; and T00,S02,$6D ==
         !byte $BD,$29,$BA
         bcs   .checkwrite
         lda   #$02
         ldx   #$82
         ldy   #$03
         jsr   compare    ; and T00,S02,$82 ==
         !byte $BD,$29,$BA
         bcs   .checkwrite
         lda   #$02
         ldx   #$95
         ldy   #$03
         jsr   compare    ; and T00,S02,$95 ==
         !byte $BD,$29,$BA
         bcs   .checkwrite
         jsr   .fixwrite
.checkwrite
         lda   #$03
         ldx   #$09
         ldy   #$03
         jsr   compare    ; if T00,S03,$09 ==
         !byte $59,$00,$BA
         bcs   .exit
         lda   #$03
         ldx   #$1A
         ldy   #$03
         jsr   compare    ; and T00,S03,$1A ==
         !byte $59,$00,$BA
         bcs   .exit
         lda   #$03
         ldx   #$2A
         ldy   #$03
         jsr   compare    ; and T00,S03,$2A ==
         !byte $D9,$00,$BA
         bcs   .exit
         jsr   .fixread
.exit
         jmp   .exit2

.fixread
         lda   #$6A
         sta   .loopcheck+1
         lda   #$96
         ldx   #<(gNIBTableBase+$16)
         ldy   #>(gNIBTableBase+$16)
         bne   .common

.fixwrite
         lda   #$40
         sta   .loopcheck+1
         lda   #$29
         ldx   #<.writedata
         ldy   #>.writedata

.common
         sta   nibdestindex
         stx   .nibload1+1
         sty   .nibload1+2
         stx   .nibload2+1
         sty   .nibload2+2
         lda   #$00
         sta   nibsrcindex
.modloop
         ldy   nibsrcindex
         ldx   nibdestindex

         ;loop while match succeeds

.nibload1
         lda   $D1D1,y
         cmp   (BASEPAGE*256)+$400,x
         beq   .nibcheck

         ;count mismatches until match or end

         inx
         iny
         cpy   .loopcheck+1
         bne   .nibload1

         ;if we saw mismatches...

.nibcheck
         cpy   nibsrcindex
         beq   .advance

         ;remember where

         txa
         pha
         tya
         pha

         ;determine how many

         sec
         sbc   nibsrcindex
         pha
         tax

         ;construct dynamic data to apply mods

         lda   #$20
         sta   $300
         lda   #<modify
         sta   $301
         lda   #>modify
         sta   $302
         lda   #$60
         sta   $303,x
.nibdec
         dex
         dey
.nibload2
         lda   $D1D1,y
         sta   $303,x
         txa
         bne   .nibdec
         pla
         tay
         lda   #$04
         ldx   nibdestindex
         jsr   $300

         ;restore position, then resume

         pla
         sta   nibsrcindex
         pla
         sta   nibdestindex
.advance
         inc   nibsrcindex
         inc   nibdestindex
         lda   nibsrcindex
.loopcheck
         cmp   #$D1
         bcc   .modloop
         rts
.exit2
}
