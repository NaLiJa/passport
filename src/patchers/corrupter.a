;-------------------------------
; #CORRUPTER
; protection check on T13,S0E
; intentionally destroys T00,S00
; if check fails
;
; Millionaire
; Squire
; Baron
; Super Huey
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         lda   gTrack
         cmp   #$13
         bne   .exit

         lda   #$0E
         ldx   #$2C
         ldy   #$07
         jsr   compare
         !byte $20,$E3,$03; JSR $03E3
         !byte $84,$00    ; STY $00
         !byte $85,$01    ; STA $01
         bcs   .exit
         ldx   #$D4
         ldy   #$0F
         jsr   compare
         !byte $A9,$00    ; LDA #$00
         !byte $8D,$00,$A0; STA $4000
         !byte $EE,$D3,$40; INC $40D3
         !byte $AD,$D3,$40; LDA $40D3
         !byte $C9,$FF    ; CMP #$FF
         !byte $D0,$F1    ; BNE -$F1
         bcs   .exit
         ldx   #$A4
         ldy   #$0F
         jsr   compare
         !byte $A9,$23    ; LDA #$23
         !byte $85,$02    ; STA $02
         !byte $A9,$00    ; LDA #$00
         !byte $85,$03    ; STA $03
         !byte $A9,$01    ; LDA #$01
         !byte $85,$04    ; STA $04
         !byte $20,$28,$40; JSR $4028
         bcs   .exit
         lda   #s_corrupter
         jsr   PrintByID
         lda   #$0E
         ldy   #$01
         jsr   modify
         !byte $60        ; RTS
.exit
}
