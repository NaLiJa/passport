;-------------------------------
; #SSI protections
; weak bit, magic values
;
; module by qkumba
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
         ldy   gIsRDOS13
         bne   .exit

         ldy   #$0F
         jsr   SearchTrack
         !byte WILDCARD,$00; xxx $00 (sometimes DEC, sometimes CMP)
         !byte $F0,$17    ; BEQ *+$19
         !byte $20,$65,$BC; JSR $BC65
         !byte $B0,$F7    ; BCS *-7
         !byte $C1,$00    ; CMP ($00,X)
         !byte $C1,$00    ; CMP ($00,X)
         !byte $EA        ; NOP
         !byte $EA        ; NOP
         bcs   .exit

         sta   gDisplayBytes
         pha
         lda   #s_ssi
         jsr   PrintByID
         pla
         ldy   #$01
         jsr   modify
         !byte $A9        ; LDA #$..
.exit
}
