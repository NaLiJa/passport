;-------------------------------
; #D5D5F7
; nibble count with weird bitstream
; involving $D5 and $F7 as delimiters
;
; Ace Detective
; Cat 'n Mouse
; Cotton Tales
; Dyno-Quest
; Easy Street
; Fraction-oids
; Math Magic
; RoboMath
; NoteCard Maker (Pascal variant)
;-------------------------------
!zone {
         bit   gMode      ; nothing to do here in verify-only mode
         bpl   .exit
;
; always run on Pascal disks
;
         lda   gIsPascal
         beq   +
;
; if DOS 3.3-shaped bootloader, only run if we found
; specific markers on T00,S00 earlier that are shared
; by all the samples I have that use this protection.
; (This filter is subject to revision if we find
; additional samples.)
;
         lda   gIsBoot0
         ora   gPossibleD5D5F7
         bne   .exit

+        ldy   #$20
         jsr   SearchTrack
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $48        ; PHA
         !byte $68        ; PLA
         !byte $C9,$D5    ; CMP #$D5
         !byte $D0,$F5    ; BNE -$F5
         !byte $A0,$00    ; LDY #00
         !byte $8C,WILDCARD,WILDCARD; STY $xxxx
         !byte $BD,$8C,$C0; LDA $C08C,X
         !byte $10,$FB    ; BPL -$FB
         !byte $C9,$D5    ; CMP #$D5
         !byte $F0,$0F    ; BEQ +$0F
         !byte $C9,$F7    ; CMP #$F7
         !byte $D0,$01    ; BNE +$01
         !byte $C8        ; INY
         !byte $18        ; CLC
         !byte $6D        ; ADC
         bcs   .exit      ; passport-test-suite/RoboMath.woz [C=0] matches
         sta   gDisplayBytes ; passport-test-suite/Notecard Maker.woz [C=0] matches
         pha
         lda   #s_d5d5f7
         jsr   PrintByID
         pla
         ldy   #$01
         jsr   modify
         !byte $60        ; RTS
.exit
}
