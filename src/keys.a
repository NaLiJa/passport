;-------------------------------
; WaitForKey
; wait forever for a key
; if the key pressed is '*', break to the monitor
; with a message on how to get back
;
; in:    none
; out:   A = uppercase version of key pressed
;        other flags and registers clobbered
;-------------------------------
WaitForKey
         bit   STROBE
-        lda   KEY
         bpl   -
         bit   STROBE
         cmp   #"*"
         bne   +
         jsr   TEXT
         jsr   PrintByID
         !byte s_reset
         jmp   $FF59
+        cmp   #$E1
         bcc   +
         and   #$DF       ; force uppercase
+        rts
