;-------------------------------
; IDDOS33
; identify DOS 3.3 bootloader or some variation
; that can be safely boot traced
;
; in:  $0800..$08FF contains T00,S00
;      first page of track buffer also contains T00,S00
; out: C clear if traceable DOS 3.3-style bootloader found
;      C set otherwise
;      all registers clobbered
;      all other flags clobbered
;-------------------------------
!zone {
IDDOS33
;
; $08FE must be either $36 or $B6 (prevents matching on some
; Infocom disks that would otherwise match but then load
; boot1 over program space and crash)
;
         lda   $08FE
         cmp   #$36
         beq   +
         cmp   #$B6
         beq   +
         sec
         rts
;
; Code at $0801 must be standard (with one exception)
;
+        lda   #$00
         ldx   #$00
         ldy   #$4A
         jsr   compare    ; if T00,S00,$00 ==
         !byte $01
         !byte $A5,$27
         !byte $C9,$09
         !byte $D0,$18
         !byte $A5,$2B
         !byte $4A
         !byte $4A
         !byte $4A
         !byte $4A
         !byte $09,$C0
         !byte $85,$3F
         !byte $A9,$5C
         !byte $85,$3E
         !byte $18
         !byte $AD,$FE,$08
         !byte $6D,$FF,$08
         !byte WILDCARD   ; will check these later
         !byte WILDCARD
         !byte WILDCARD
         !byte $AE,$FF,$08
         !byte $30,$15
         !byte $BD,$4D,$08
         !byte $85,$3D
         !byte $CE,$FF,$08
         !byte $AD,$FE,$08
         !byte $85,$27
         !byte $CE,$FE,$08
         !byte $A6,$2B
         !byte $6C,$3E,$00
         !byte $EE,$FE,$08
         !byte $EE,$FE,$08
         !byte $20,$89,$FE
         !byte $20,$93,$FE
         !byte $20,$2F,$FB
         !byte $A6,$2B
         bcs   .exit
;
; Sector order map must be standard (no exceptions)
;
         lda   #$00
         ldx   #$4D
         ldy   #$10
         jsr   compare    ; if T00,S00,$4D ==
         !byte $00,$0D,$0B,$09,$07,$05,$03,$01
         !byte $0E,$0C,$0A,$08,$06,$04,$02,$0F
         bcs   .exit
;
; Minor variant (e.g. Terrapin Logo 3.0) jumps to $08F0 and back
; but is still safe to trace. Check for this jump and match
; the code at $08F0 exactly.
;
         lda   #$00
         ldx   #$1C
         ldy   #$03
         jsr   compare    ; if T00,S00,$1C ==
         !byte $8D,$FE,$08
         bcc   .exit      ; standard code at $081C -> success

         lda   #$00
         ldx   #$1C
         ldy   #$03
         jsr   compare    ; if T00,S00,$1C ==
         !byte $4C,$F0,$08; JMP $08F0
         bcs   .exit      ; unknown code at $081C -> failure

         lda   #$00
         ldx   #$F0
         ldy   #$09
         jsr   compare    ; if T00,S00,$F0 ==
         !byte $8D,$FE,$08
         !byte $EE,$F3,$03
         !byte $4C,$1F,$08
;        bcs   .exit      ; unknown code at $08F0 -> failure

.exit
         rts
}
