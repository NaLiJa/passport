;-------------------------------
; IDVolumeNameProDOS
; identify and print ProDOS volume name if found
;
; in:  $0800..$08FF contains T00,S00
;      first page of track buffer also contains T00,S00
;      gIsProDOS is true
; out: C clear if disk volume name was found and printed
;      C set otherwise
;      oVerflow always clear
;      all registers clobbered
;      all other flags clobbered
;-------------------------------
!zone {
IDVolumeNameProDOS
; read T00,S0B (volume key block)
         lda   #$00
         sta   gTrack
         sta   cmp1
         lda   #$0B
         sta   gSector
         sta   gDisplayBytes
         clc
         adc   #BASEPAGE
         sta   gAddress+1
         sta   cmp1+1
         jsr   ReadSector
         bcs   .error

; sanity check the volume name length byte
         ldy   #$04
         lda   (cmp1), y
         cmp   #$F1
         bcc   .error
         and   #$0F
         tax
; X (now the volume name length) is preserved by the PrintByID call
         lda   #s_volumename
         jsr   PrintByID

; print the volume name
         ldy   #$05
-        lda   (cmp1), y
         ora   #$80
         jsr   PrintA
         iny
         dex
         bne   -
         lda   #$8D
         jsr   PrintA
         clc
         !byte $24        ; hide next SEC
.error
         sec
         clv
         rts
}
