;-------------------------------
; IDBootloader
; main entry point to identify the bootloader
; and exit via the appropriate tracer/copier
;
; in:    T00,S00 in memory at $0800
;-------------------------------
IDBootloader
;
; Reset all per-disk globals.
; Some of these are used as filters later so irrelevant patchers can be skipped.
;
         ldx   #(LASTFILTER-FIRSTFILTER)
         lda   #FALSE
-        sta   FIRSTFILTER-1, x
         dex
         bne   -

         ; X = 0
         stx   gMECCFastloadType
         stx   gLastTrack

         txa
         ; A = 0
         ldx   #9
-        sta   gDisplayBytes, x
         dex
         bpl   -

         ldx   #$0F
         ldy   #$F8
-        tya
         sta   precheck_sectors, x
         iny
         dex
         bpl   -

         lda   #s_infocom18
         ldx   gIsInfocom18
         beq   .printinfo

         lda   #s_rw18
         ldx   gIsRW18
         beq   .printinfo

.check13
         lda   gIs13Sector
         bne   .sanity
         lda   #s_13sector

.printinfo
         sta   +
         jsr   PrintByID
+        !byte $FD                   ; SMC
         jmp   UseUniversal

.sanity
;
; Quick sanity check -- only recognized values for $0800 are
; $01 = most disks
; $02 = Special Delivery (DOS 3.3P)
; $05 = some EA disks
;
         lda   $0800
         beq   +
         cmp   #$03
         bcc   .sane
         cmp   #$05
         beq   .sane
+        jmp   UseUniversal

.sane
;
; Copy the boot sector from $0800 to the track/sector buffer
; so we can reuse our standard compare functions.
;
         lda   #$08
         ldx   #BASEPAGE
         ldy   #$01
         jsr   CopyMemory            ; exits with X=0
         txa                         ; A=0
;
; Try to identify DOS 3.3-shaped bootloader.
; Exit via custom trace function if found.
;
         jsr   IDDOS33
         bcs   .notdos33
         lda   #TRUE
         sta   gIsBoot0
         +IDD5D5F7
         +IDDiversi
         ldx   #s_diversidos
         bcc   .printdos
         +IDPronto
         ldx   #s_prontodos
         bcc   .printdos
         ldx   #s_dosb0

.printdos
         stx   +
         jsr   PrintByID
+        !byte $FD                   ; SMC
         jmp   TraceDOS33
.notdos33
;
; Try to identify a variant bootloader that calls to $08B3
; early to munge the nibble tables used by the drive firmware.
; Exit via custom trace function if found.
;
         +ID8b3
         bcs   .not8b3
         jsr   PrintByID
         !byte s_jsr8b3
         sta   gIs8b3
         jmp   Trace8B3
.not8b3
;
; Try to identify Laureate Learning Systems bootloader.
; Exit via custom trace function if found.
;
         +IDLaureate
         bcs   .notLaureate
         jsr   PrintByID
         !byte s_laureate
         lda   #TRUE
         sta   gIsLaureate
         lda   #$0B
         jsr   PrereadT00Partial
         bcs   .notLaureate
         jmp   TraceLaureate
.notLaureate
;
; Try to identify all the different MECC fastloader variants.
; Exit via custom trace function if found.
;
         +IDMECC
         bcs   .notmecc
         lda   #ID_MECC_UNK
         sta   gMECCFastloadType
         jsr   PrintByID
         !byte s_mecc
         jsr   PrereadT00
         bcs   .notmecc
         jsr   IDMECC1
         bcs   +
         lda   #ID_MECC1
         sta   gMECCFastloadType
         jmp   foundmecc1
+        jsr   IDMECC2
         bcs   +
         lda   #ID_MECC2
         sta   gMECCFastloadType
         jmp   foundmecc2
+        jsr   IDMECC3
         bcs   +
         lda   #ID_MECC3
         sta   gMECCFastloadType
         jmp   foundmecc3
+        jsr   IDMECC4
         bcs   .notmecc
         lda   #ID_MECC4
         sta   gMECCFastloadType
         jmp   foundmecc4
.notmecc
;
; Try to identify DOS 3.3P (Special Delivery) bootloader.
; Exit via custom trace function if found.
;
         +IDSpecDel
         bcs   .notSpecialDelivery
         jsr   PrintByID
         !byte s_specdel
         jmp   TraceSpecDel
.notSpecialDelivery
;
; Try to identify Electronic Arts bootloader.
; Exit via custom trace function if found.
;
         +IDEA
         bcs   .notEA
         jmp   foundea
.notEA
;
; Try to identify hybrid 16-/13-sector bootloader on
; DOS 3.2 disks.
; Exit via custom trace function if found.
;
         +IDDOS32
         bcs   .notDOS32
         jsr   PrintByID
         !byte s_dos32b0
         jmp   TraceDOS32
.notDOS32
         +IDDOS32LO
         bcs   .notDOS32LO
         jsr   PrintByID
         !byte s_dos32b0
         jmp   TraceDOS32LO
.notDOS32LO
;
; Try to identify Interplay bootloader.
; Exit via custom trace function if found.
;
         +IDAdvent
         bcs   .notInterplay
         jmp   foundadvent
.notInterplay
;
; Try to detect whether there is code in the boot sector
; that loads 4-and-4-encoded data. This is an early escape
; hatch for disks that will fail later anyway.
;
         +IDEncoded44
         bcs   .not44
         jsr   PrintByID
         !byte s_encoded44
         jmp   TheEnd
.not44
;
; Try to detect whether there is code in the boot sector
; that loads 5-and-3-encoded data. If the earlier DOS 3.2
; identification failed, we will have no way to read the disk
; (the universal RWTS only works on 6-and-2 encoded data), so
; this serves as an early escape hatch for disks that will
; fail later anyway.
;
         +IDEncoded53
         bcs   .not53
         jsr   PrintByID
         !byte s_encoded53
         jmp   TheEnd
.not53
;
; ProDOS (all versions)
;
         +IDProDOS
         bcs   .notProDOS
         jsr   PrintByID
         !byte s_prodosb0
         lda   #TRUE
         sta   gIsProDOS
         jsr   IDVolumeName
         ; /!\ execution falls through here because Dinkey-DOS disks are also ProDOS
.notProDOS
;
; Dinkey-DOS (ProDOS file structure with DOS 3.3-ish RWTS in language card)
; detectable now because IDVolumeName just read the first sector of the
; volume directory into memory so we can look for a unique filename
;
         +IDDinkeyDOS
         bcs   .notDinkey
         lda   #$0B
         sta   gDisplayBytes
         jsr   PrintByID
         !byte s_dinkeydos
         lda   #TRUE
         sta   gIsDinkeyDOS
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notDinkey
;
; Apple Pascal (all versions)
;
         +IDPascal
         bcs   .notPascal
         jsr   PrintByID
         !byte s_pascalb0
         lda   #TRUE
         sta   gIsPascal
         jsr   IDVolumeName
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notPascal
;
; David-DOS II
;
         +IDDavid
         bcs   .notDavid
         jsr   PrintByID
         !byte s_daviddos
         lda   #TRUE
         sta   gIsDavidDOS
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notDavid
;
; Encrypted Datasoft bootloader
;
         +IDDatasoft
         bcs   .notDatasoft
         jsr   PrintByID
         !byte s_datasoftb0
         lda   #TRUE
         sta   gIsDatasoft
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notDatasoft
;
; TSR bootloader
;
         +IDTSR
         bcs   .notTSR
         jsr   PrintByID
         !byte s_tsr
         lda   #TRUE
         sta   gIsTSR
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notTSR
;
; Micrograms bootloader
; (just for display)
;
         +IDMicrograms
         bcs   .notMicrograms
         jsr   PrintByID
         !byte s_micrograms
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notMicrograms
;
; Quick-DOS
; (just for display)
;
         +IDQuickDOS
         bcs   .notQuickDOS
         jsr   PrintByID
         !byte s_quickdos
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notQuickDOS
;
; RDOS (16-sector)
; (just for display)
;
+
         +IDRDOS16
         bcs   .notRDOS16
         jsr   PrintByID
         !byte s_rdos
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notRDOS16
;
; Dav Holle encrypted bootloader
; (will be decrypted and patched)
;
         jsr   IDHolle
         bcs   .notHolle
         jsr   PrintByID
         !byte s_holle
         lda   #TRUE
         sta   gIsHolle
         ; /!\ execution falls through here to save bytes (no other IDs will match)
.notHolle
;
; 555 bootloader
;
         jsr   ID555
         bcs   .not555
         lda   #0
         sta   gDisplayBytes
         jsr   PrintByID
         !byte s_555
         jmp   Do555
.not555
         jmp   UseUniversal

PrereadT00
         lda   #$0F
PrereadT00Partial
         sta   gSector
         clc
         adc   #BASEPAGE
         sta   gAddress+1
         lda   #$00
         sta   gTrack
         sta   gAddress
-        jsr   ReadSector
         bcs   +
         dec   gAddress+1
         dec   gSector
         bne   -
+        rts
