;-------------------------------
; Trace
; common code for all boot tracers
; in:  A = lo byte of callback
;      X = hi byte of callback
; never returns (exits via callback or UseUniversal)
;-------------------------------
!zone {
Trace
         pha
         txa
         pha
         lda   #$C6
         ldx   #$B6
         ldy   #$01
         jsr   CopyMemory
         pla
         ldx   #0
         ldy   $B6F8
         cpy   #$4C
         beq   +
         ldx   #3
         ldy   $B6FB
         cpy   #$4C
         bne   ++
+        sta   $B6FA,x               ; hi byte of callback
         pla
         sta   $B6F9,x               ; lo byte of callback
         jmp   $B600

++       pla
         jsr   PrintByID             ; unknown disk controller
         !byte s_canttrace
         jmp   UseUniversal
}
