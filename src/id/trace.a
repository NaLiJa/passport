;-------------------------------
; Trace
; common code for all boot tracers
; in:  A = lo byte of callback
;      X = hi byte of callback
; never returns (exits via callback or UseUniversal)
;-------------------------------
!zone {
Trace
         pha
         txa
         pha
         lda   #$C6
         ldx   #$B6
         ldy   #$01
         jsr   CopyMemory
         pla
         tax
         pla
         ldy   $B6F8
         cpy   #$4C
         bne   +
         sty   $B6F8      ; JMP for Disk II controller
         sta   $B6F9      ; lo byte of callback
         stx   $B6FA      ; hi byte of callback
         jmp   $B600
+        ldy   $B6FB
         cpy   #$4C
         bne   +
         sty   $B6FB      ; JMP for IIgs smart controller
         sta   $B6FC      ; lo byte of callback
         stx   $B6FD      ; hi byte of callback
         jmp   $B600

+        lda   #s_canttrace ; unknown disk controller
         jsr   PrintByID
         jmp   UseUniversal
}
