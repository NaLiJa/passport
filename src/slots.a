DiskIIArray
         !byte 00,00,00,00,00,00,00
HardDiskArray                                   ; must be immediately after DiskIIArray
         !byte 00,00,00,00,00,00,00
         !byte 00,00,00,00,00,00,00,00

;-------------------------------
; NextSlot
; in:    DiskIIArray, HardDiskArray filled by calling
;          ScanForDiskII and ScanForRAMAndHardDisks
; out:   @SLOT, @DRIVE set to ASCII values of
;          next available slot/drive
;        @g
;-------------------------------
!zone {
NextSlot
         ldy   #FALSE
         lda   DRIVE
         cmp   #$31
         bne   .incslot
         lda   #$32
         sta   DRIVE
         bne   .getslot   ; unconditional branch
.incslot
         lda   #$31
         sta   DRIVE
         inc   SLOT
.getslot
         lda   SLOT
         cmp   #$38
         bne   .nowrap
         lda   #$31
         sta   SLOT
.nowrap
         sec
         sbc   #$30
         tax
         lda   DiskIIArray-1,x
         bne   .done
         lda   DRIVE
         lsr
         bcs   .checkdrive
         txa
         ora   #8
         tax
.checkdrive
         lda   HardDiskArray-1,x
         beq   .incslot
         jsr   SwapProDOS            ; preserves A
         jsr   GetVolumeName
         jsr   SwapProDOS
         ldy   #TRUE
.done
         sty   gUsingHardDisk
         lda   SLOT
         cmp   #$36
         bne   .reallydone
         lda   #$32
         sta   DRIVE
.reallydone
         rts
}
